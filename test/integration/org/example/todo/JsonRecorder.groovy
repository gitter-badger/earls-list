package org.example.todo

import grails.converters.JSON
import org.codehaus.groovy.grails.web.json.JSONElement
import org.codehaus.groovy.grails.web.json.JSONObject

class JsonRecorder {


    private final String fixturesRoot
    private final String domainClass

    private String fixturesSuffix = "gen.fixture.js"


    private JsonRecorder(String fixturesRoot, String domainClass) {
        this.fixturesRoot = fixturesRoot
        this.domainClass = domainClass
    }

    static JsonRecorder create(String jsonRoot, String domainClass) {
        return new JsonRecorder(jsonRoot, domainClass)

    }

    Fixture leftShift(String fixtureName) {
        new Fixture(fixtureName: fixtureName, recorder: this)
    }


    JSONElement rec(String fixtureName, JSONElement json) {
        File fixtureFile = newFixture(fixturesRoot, "${domainClass}/fixtures", fixtureName, fixturesSuffix)
        fixtureFile.text = getFixtureText(domainClass, fixtureName, new JSON(json).toString(true))
        json
    }

    JSONObject rec(String fixtureName, JSONObject json) {
        rec(fixtureName, json as JSONElement)
    }

    Map rec(String fixtureName, Map json) {
        File fixtureFile = newFixture(fixturesRoot, "${domainClass}/fixtures", fixtureName, fixturesSuffix)
        fixtureFile.text = getFixtureText(domainClass, fixtureName, new JSON(json).toString(true))
        json
    }

    String rec(String fixtureName, String json) {
        File fixtureFile = newFixture(fixturesRoot, "${domainClass}/fixtures", fixtureName, fixturesSuffix)
        fixtureFile.text = getFixtureText(domainClass, fixtureName, json)
        json

    }


    private static File newFixture(String filePath, prefix, fixtureName, suffix){
        def fixtureFile =  new File("${filePath}/${prefix}/${fixtureName}.${suffix}")
        fixtureFile.parentFile.mkdirs()
        return fixtureFile
    }

    private static String getFixtureText(String domainClass, String fixtureName, String json){
        return """/** Generated automatically from $domainClass. Do not edit this file manually! */
    (function (window) {
        window['fixtures'] = window['fixtures'] || {};
        var fixtures = window['fixtures'];
        fixtures['$domainClass'] = fixtures['$domainClass'] || {};
        var $domainClass = fixtures['$domainClass'];
        window.fixtures.$domainClass.$fixtureName = $json;
    })(window);"""
    }

}

class Fixture {

    JsonRecorder recorder
    String fixtureName

    JSONObject leftShift(JSONObject json) {
        recorder.rec(fixtureName, json)
    }

    JSONElement leftShift(JSONElement json) {
        recorder.rec(fixtureName, json)
    }

    String leftShift(String json) {
        recorder.rec(fixtureName, json)
    }

    Map leftShift(Map json) {
        recorder.rec(fixtureName, json)
    }
}
